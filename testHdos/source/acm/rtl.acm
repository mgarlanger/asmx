	SPACE	4,10
**	$RTL - READ TEXT LINE.
*
*	$RTL READS A LINE FROM THE TERMINAL.
*
*	CHARACTER ARE ACCEPTED FROM THE TERMINAL, RUBOUT AND BACKSPACE
*	CHARACTERS ARE PROCESSED. WHEN A CARRIAGE RETURN IS ENTERED, 
*	$RTL RETURNS.
*
*	ENTRY	(HL) = BUFFER FWA
*	EXIT	'C' CLEAR IF OK
*		 DATA IN BUFFER
*		 (A) = TEXT LENGTH
*		'C' SET IF CTL-D STRUCK
*	USES	A,F

$RTL.	CALL	$RTL			; $RTL IN UPPER CASE
	RC				; CTL-D
	JMP	$MLU			; MAP LINE TO UPPER CASE

$RTL	EQU	*

	PUSH	H			; SAVE FWA
$RTL1	SCALL	.SCIN
	JC	$RTL1
	CPI	CTLD
	JE	$RTL2			; CTL-D STRUCK
	MOV	M,A
	INX	H
	CPI	NL
	JNE	$RTL1
	DCX	H
	MVI	M,0
	INX	H

*	ALL DONE. COMPUTE LENGTH

	XCHG				; (DE) = LWA+1
	XTHL				; (HL) = FWA
	MOV	A,E
	SUB	L			; (A) = LENGTH
	ANA	A			; CLEAR CARRY
	POP	D			; RESTORE (DE)
	RET

*	CTL-D STRUCK

$RTL2	POP	H			; (HL) = FWA
	STC
	RET
