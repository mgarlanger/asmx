	SPACE	4,10
**	$ICTT - INPUT FROM CONSOLE TASK TIME.
*
*	$ICTT IS A TASK-TIME CONSOLE INPUT ROUTINE, WHICH
*	PERFORMS SIMPLE SINGLE CHARACTER INPUTS.
*
*	IT IS CALLED DURING BOOT OPERATIONS, AND BY SPECIAL ROUTINES
*	WHICH MAY BE RUNNING IN ENVIRONMENTS WHERE KEYBOARD INTERRUPTS
*	ARE UNDESIRABLE.
*
*	Modified to handle H8-4 ports by G. Chandler, 1-SEP-78
*	This routine assumes that the ports have been previously initialized,
*	and that S.CDB has been previously initialized.
*
*	ENTRY	NONE
*	EXIT	(A) = CHARACTER
*	USES	A,F

$ICTT	CALL	$ICTT.
	JC	$ICTT
	CALL	$ICTT..
	RET

$ICTT.	LDA	S.CDB
	CPI	CDB.H84
	JZ	ICTT2			; IF  H8-4 PORT

*	HAVE 8251 FOR CONSOLE

ICTT1	IN	SC.UART+USR
	ANI	USR.RXR
	RNZ				; READY

	STC				; FLAG NOT READY
	RET

*	HAVE 8250 PORT FOR CONSOLE

ICTT2	IN	SC.ACE+UR.LSR
	ANI	UC.DR
	RNZ				; READY

	STC				; FLAG NOT READY
	RET

$ICTT.. LDA	S.CDB
	CPI	CDB.H84
	JZ	ICTT3

*	HAVE 8251 FOR CONSOLE

	IN	SC.UART+UDR
	ANI	177Q
	RET

*	HAVE 8250 FOR CONSOLE

ICTT3	IN	SC.ACE+UR.RBR
	ANI	177Q
	RET
